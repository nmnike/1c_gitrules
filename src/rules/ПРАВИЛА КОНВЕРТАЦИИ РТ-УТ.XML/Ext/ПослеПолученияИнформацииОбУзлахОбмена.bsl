//часто используемые данные

Параметры.Вставить("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());

Параметры.Вставить("ТипСправочникСсылкаНоменклатура"  , Тип("СправочникСсылка.Номенклатура")); 
Параметры.Вставить("ТипСправочникСсылкаКонтрагенты"   , Тип("СправочникСсылка.Контрагенты")); 
Параметры.Вставить("ТипСправочникСсылкаОрганизации"   , Тип("СправочникСсылка.Организации")); 
Параметры.Вставить("ТипСправочникСсылкаСклады"        , Тип("СправочникСсылка.Склады")); 
Параметры.Вставить("ТипСправочникСсылкаФизическиеЛица", Тип("СправочникСсылка.ФизическиеЛица")); 

//произвольная организация - первая разрешенная
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
РезультатЗапроса = Запрос.Выполнить();
Если РезультатЗапроса.Пустой() Тогда
	Параметры.Вставить("РазрешеннаяОрганизация", Справочники.Организации.ПустаяСсылка());
Иначе
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Параметры.Вставить("РазрешеннаяОрганизация", Выборка.Ссылка);
КонецЕсли;

	
//построим соответствия кодов магазинов и реквизитов
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов.КодМагазина,
	|	ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов.СкладМагазина,
	|	ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов.КассаМагазина,
	|	ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов.КассаККММагазина,
	|	ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов.Организация
	|ИЗ
	|	ПланОбмена.ОбменУправлениеТорговлейРозничнаяТорговля.СоответствияМагазиновИСкладов КАК ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов
	|ГДЕ
	|	ОбменУправлениеТорговлейРозничнаяТорговляСоответствияМагазиновИСкладов.Ссылка = &УзелОбменаЗагрузкаДанных";
	
Запрос.УстановитьПараметр("УзелОбменаЗагрузкаДанных", УзелОбменаЗагрузкаДанных);
	
СоответствиеКодСклад       = Новый Соответствие;
СоответствиеКодКасса       = Новый Соответствие;
СоответствиеКодКассаККМ    = Новый Соответствие;
СоответствиеКодОрганизация = Новый Соответствие;
	
Выборка = Запрос.Выполнить().Выбрать();
Пока Выборка.Следующий() Цикл
	
	СоответствиеКодСклад.Вставить(Выборка.КодМагазина, Выборка.СкладМагазина);
	СоответствиеКодКасса.Вставить(Выборка.КодМагазина, Выборка.КассаМагазина);
	СоответствиеКодКассаККМ.Вставить(Выборка.КодМагазина, Выборка.КассаККММагазина);
	СоответствиеКодОрганизация.Вставить(Выборка.КодМагазина, Выборка.Организация);
	
КонецЦикла;

Параметры.Вставить("СоответствиеКодСклад"      , СоответствиеКодСклад);
Параметры.Вставить("СоответствиеКодКасса"      , СоответствиеКодКасса);
Параметры.Вставить("СоответствиеКодКассаККМ"   , СоответствиеКодКассаККМ);
Параметры.Вставить("СоответствиеКодОрганизация", СоответствиеКодОрганизация);


УзелОбъект = УзелОбменаЗагрузкаДанных.ПолучитьОбъект();
//фиктивный вызов для заполнения таблиц
ФиктивнаяОтправка = ОтправкаЭлементаДанных.Авто;
УзелОбъект.ОпределитьТипОтправкиДанных(Справочники.Номенклатура.ПустаяСсылка(), ФиктивнаяОтправка);
Параметры.Вставить("УзелДляОбменаОбъект", УзелОбъект);
