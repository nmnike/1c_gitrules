Запрос = Новый Запрос("
|ВЫБРАТЬ
|	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента,
|	
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка                 КАК Ссылка,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.Номер           КАК Номер,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.Дата            КАК Дата,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.Проведен        КАК Проведен,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.ПометкаУдаления КАК ПометкаУдаления,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.Комментарий   КАК Комментарий,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.Контрагент    КАК Контрагент,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Ссылка.Ответственный КАК Ответственный,
|
|	УстановкаЦенНоменклатурыКонтрагентовТовары.ТипЦен                     КАК ТипЦен,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Номенклатура               КАК Номенклатура,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.Цена                       КАК Цена,
|	УстановкаЦенНоменклатурыКонтрагентовТовары.ЕдиницаИзмерения           КАК ЕдиницаИзмерения
|ИЗ
|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаЦенНоменклатурыКонтрагентов.Товары КАК УстановкаЦенНоменклатурыКонтрагентовТовары
|        ПО УстановкаЦенНоменклатурыКонтрагентовТовары.ТипЦен = ДоговорыКонтрагентов.ТипЦен
|ГДЕ
|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)
|ИТОГИ ПО
|	ДоговорКонтрагента,
|	Ссылка,
|	ТипЦен
|");

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Проведен");
ВыборкаДанных.Колонки.Добавить("ПометкаУдаления");
ВыборкаДанных.Колонки.Добавить("Ответственный");
ВыборкаДанных.Колонки.Добавить("Комментарий");

ВыборкаДанных.Колонки.Добавить("Партнер");
ВыборкаДанных.Колонки.Добавить("Соглашение");

ВыборкаДанных.Колонки.Добавить("Товары");


РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоДоговорамКонтрагента = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоДоговорамКонтрагента.Следующий() Цикл
	
	ВыборкаПоУстановкамЦенНоменклатуры = ВыборкаПоДоговорамКонтрагента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоУстановкамЦенНоменклатуры.Следующий() Цикл
		
		ВыборкаПоТипамЦен = ВыборкаПоУстановкамЦенНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоТипамЦен.Следующий() Цикл
			
			НоваяСтрока = ВыборкаДанных.Добавить();
			НоваяСтрока.Дата            = НачалоДня(ВыборкаПоУстановкамЦенНоменклатуры.Дата); 
			НоваяСтрока.Проведен        = ВыборкаПоУстановкамЦенНоменклатуры.Проведен; 
			НоваяСтрока.ПометкаУдаления = ВыборкаПоУстановкамЦенНоменклатуры.ПометкаУдаления; 
			НоваяСтрока.Ответственный   = ВыборкаПоУстановкамЦенНоменклатуры.Ответственный; 
			НоваяСтрока.Комментарий     = ВыборкаПоУстановкамЦенНоменклатуры.Комментарий; 
			
			НоваяСтрока.Партнер    = ВыборкаПоУстановкамЦенНоменклатуры.Контрагент; 
			НоваяСтрока.Соглашение = ВыборкаПоДоговорамКонтрагента.ДоговорКонтрагента; 
			
			НоваяСтрока.Товары = Новый ТаблицаЗначений;
			НоваяСтрока.Товары.Колонки.Добавить("Номенклатура");
			НоваяСтрока.Товары.Колонки.Добавить("Характеристика");
			НоваяСтрока.Товары.Колонки.Добавить("Упаковка");
			НоваяСтрока.Товары.Колонки.Добавить("УсловиеЦеныПоставщика");
			НоваяСтрока.Товары.Колонки.Добавить("Цена");
			
			ВыборкаПоДетальнымЗаписям = ВыборкаПоТипамЦен.Выбрать();
			Пока ВыборкаПоДетальнымЗаписям.Следующий() Цикл
				
				НоваяСтрокаТовары = НоваяСтрока.Товары.Добавить();	
				НоваяСтрокаТовары.Номенклатура               = ВыборкаПоДетальнымЗаписям.Номенклатура; 
				НоваяСтрокаТовары.Характеристика             = ВыборкаПоДетальнымЗаписям.ХарактеристикаНоменклатуры; 
				НоваяСтрокаТовары.Упаковка                   = ВыборкаПоДетальнымЗаписям.ЕдиницаИзмерения; 
				
				НоваяСтрокаТовары.УсловиеЦеныПоставщика = Новый Структура;
				НоваяСтрокаТовары.УсловиеЦеныПоставщика.Вставить("Владелец",        ВыборкаПоДоговорамКонтрагента.ДоговорКонтрагента);
				НоваяСтрокаТовары.УсловиеЦеныПоставщика.Вставить("Наименование",    ВыборкаПоДетальнымЗаписям.ТипЦен.Наименование);
				НоваяСтрокаТовары.УсловиеЦеныПоставщика.Вставить("ПометкаУдаления", ВыборкаПоДетальнымЗаписям.ТипЦен.ПометкаУдаления);
				
				НоваяСтрокаТовары.Цена                       = ВыборкаПоДетальнымЗаписям.Цена; 
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецЦикла;
