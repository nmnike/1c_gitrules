Запрос = Новый Запрос("
|ВЫБРАТЬ
|	ДенежныеСредстваОстатки.Организация         КАК Организация,
|	ДенежныеСредстваОстатки.БанковскийСчетКасса КАК БанковскийСчет,
|	ДенежныеСредстваОстатки.СуммаОстаток        КАК Сумма
|ИЗ
|	РегистрНакопления.ДенежныеСредства.Остатки(&ДатаОстатков, ВидДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Безналичные)) КАК ДенежныеСредстваОстатки
|
|УПОРЯДОЧИТЬ ПО
|	ДенежныеСредстваОстатки.БанковскийСчетКасса.Наименование 	
| 
|ИТОГИ ПО
|	Организация
|");

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("БанковскиеСчета");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков); 
	НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация; 
	
	НоваяСтрока.БанковскиеСчета = Новый ТаблицаЗначений;
	НоваяСтрока.БанковскиеСчета.Колонки.Добавить("БанковскийСчет");
	НоваяСтрока.БанковскиеСчета.Колонки.Добавить("Сумма");
	
	ВыборкаДетальныеЗаписи = ВыборкаПоОрганизациям.Выбрать(); 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.Сумма < 0.00 Тогда
			СтрокаСообщения = "Обнаружены отрицательные остатки на банковском счете ""%1"" при выгрузке по правилу: ""%2""";
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1", ВыборкаДетальныеЗаписи.БанковскийСчет);
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2", "Денежные cредства на банковских счетах");
			Сообщить(СтрокаСообщения, СтатусСообщения.ОченьВажное);
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаБанковскиеСчета = НоваяСтрока.БанковскиеСчета.Добавить();	
		НоваяСтрокаБанковскиеСчета.БанковскийСчет = ВыборкаДетальныеЗаписи.БанковскийСчет;
		НоваяСтрокаБанковскиеСчета.Сумма          = ВыборкаДетальныеЗаписи.Сумма;
		
	КонецЦикла;
	
	Если НоваяСтрока.БанковскиеСчета.Количество() = 0 Тогда
		ВыборкаДанных.Удалить(НоваяСтрока);	
	КонецЕсли;
	
КонецЦикла;
