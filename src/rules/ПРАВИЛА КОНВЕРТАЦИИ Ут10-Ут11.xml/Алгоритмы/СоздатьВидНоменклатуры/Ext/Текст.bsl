
Если НЕ Параметры.Свойство("КешВидовНоменклатуры") Тогда
	Параметры.Вставить("КешВидовНоменклатуры", Новый Соответствие);
КонецЕсли;

СоздатьВидНоменклатуры_РезультатПоиска = Параметры.КешВидовНоменклатуры.Получить(СоздатьВидНоменклатуры_Номенклатура);
Если СоздатьВидНоменклатуры_РезультатПоиска <> Неопределено Тогда
	
	ИмяПКО = "ВидыНоменклатуры";
	Значение = СоздатьВидНоменклатуры_РезультатПоиска; 	
	
Иначе

	ЗапросПоСвойствамНоменклатуры = Новый Запрос("
	|ВЫБРАТЬ
	|	НазначенияСвойствОбъектов.Объект КАК Назначение,
	|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
	|	СвойстваОбъектов.Ссылка КАК Свойство
	|ИЗ
	|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
	|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО (
	|		(
	|		Номенклатура.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		)
	|		И Номенклатура.Ссылка = &Номенклатура
	|		)
	|ГДЕ
	|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура)
	|	И НазначенияСвойствОбъектов.Объект.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НазначенияСвойствОбъектов.Объект,
	|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
	|	СвойстваОбъектов.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
	|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
	|ГДЕ
	|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура)
	|	И Не НазначенияСвойствОбъектов.Объект.ЭтоГруппа
	|	И НазначенияСвойствОбъектов.Объект = &Номенклатура
	|");
	ЗапросПоСвойствамНоменклатуры.Параметры.Вставить("Номенклатура", СоздатьВидНоменклатуры_Номенклатура);

	ЗапросПоСвойствамХарактеристик = Новый Запрос("
	|ВЫБРАТЬ
	|	НазначенияСвойствОбъектов.Объект КАК Назначение,
	|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	СвойстваОбъектов.Ссылка КАК Свойство
	|ИЗ
	|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
	|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО (
	|		(
	|		Номенклатура.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
	|		)
	|		И Номенклатура.Ссылка = &Номенклатура
	|		)
	|ГДЕ
	|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры)
	|	И Номенклатура.ВестиУчетПоХарактеристикам
	|	И НазначенияСвойствОбъектов.Объект.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НазначенияСвойствОбъектов.Объект,
	|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
	|	НазначенияСвойствОбъектов.Объект,
	|	СвойстваОбъектов.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
	|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
	|ГДЕ
	|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры)
	|	И Не НазначенияСвойствОбъектов.Объект.ЭтоГруппа
	|	И НазначенияСвойствОбъектов.Объект.ВестиУчетПоХарактеристикам
	|	И НазначенияСвойствОбъектов.Объект = &Номенклатура
	//|	
	//|ИТОГИ ПО
	//|	Назначение
	|");
	ЗапросПоСвойствамХарактеристик.Параметры.Вставить("Номенклатура", СоздатьВидНоменклатуры_Номенклатура);

	РезультатЗапросаПоСвойствамХарактеристик = ЗапросПоСвойствамХарактеристик.Выполнить();
	РезультатЗапросаПоСвойствамНоменклатуры  = ЗапросПоСвойствамНоменклатуры.Выполнить();

	Если (НЕ РезультатЗапросаПоСвойствамХарактеристик.Пустой()) ИЛИ (НЕ РезультатЗапросаПоСвойствамНоменклатуры.Пустой()) Тогда
		
		ИмяПКО = "ВидыНоменклатуры";
		
		Значение = Новый Структура;
		Значение.Вставить("ПометкаУдаления", Ложь);
		Значение.Вставить("ТипНоменклатуры", СоздатьВидНоменклатуры_ТипНоменклатуры);
		
		Если СоздатьВидНоменклатуры_Номенклатура.ВестиУчетПоХарактеристикам Тогда
			Если Параметры.СворачиватьХарактеристики Тогда
				Значение.Вставить("ИспользованиеХарактеристик", "ОбщиеДляВидаНоменклатуры");
			Иначе
				Значение.Вставить("ИспользованиеХарактеристик", "ИндивидуальныеДляНоменклатуры");
			КонецЕсли;
		Иначе
			Значение.Вставить("ИспользованиеХарактеристик", "НеИспользовать");
		КонецЕсли;
		
		Значение.Вставить("НаборСвойств",              Неопределено);
		Значение.Вставить("НаборСвойствХарактеристик", Неопределено);
		
		СоответствиеНаименование = Новый Соответствие;
		ЕстьНаборСвойств = Ложь;
		ЕстьНаборСвойствХарактеристик = Ложь;
		
		Если НЕ РезультатЗапросаПоСвойствамХарактеристик.Пустой() Тогда
			
			ЕстьНаборСвойствХарактеристик = Истина;
			
			НаборСвойствХарактеристик = Новый Структура;
			Значение.Вставить("НаборСвойствХарактеристик", НаборСвойствХарактеристик);
			НаборСвойствХарактеристик.Вставить("ПометкаУдаления", Ложь);
			НаборСвойствХарактеристик.Вставить("Родитель", "Справочник_ХарактеристикиНоменклатуры");
			НаборСвойствХарактеристик.Вставить("ДополнительныеРеквизиты", Новый ТаблицаЗначений);
			
			НаборСвойствХарактеристик.ДополнительныеРеквизиты.Колонки.Добавить("Свойство");
			
			Выборка = РезультатЗапросаПоСвойствамХарактеристик.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				СоответствиеНаименование.Вставить(Выборка.НазначениеНаименование);
				
				НовыеСвойство = НаборСвойствХарактеристик.ДополнительныеРеквизиты.Добавить();
				НовыеСвойство.Свойство = Выборка.Свойство;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если НЕ РезультатЗапросаПоСвойствамНоменклатуры.Пустой() Тогда
			
			ЕстьНаборСвойств = Истина;
			
			НаборСвойств = Новый Структура;
			Значение.Вставить("НаборСвойств", НаборСвойств);
			НаборСвойств.Вставить("ПометкаУдаления", Ложь);
			НаборСвойств.Вставить("Родитель", "Справочник_Номенклатура");
			НаборСвойств.Вставить("ДополнительныеРеквизиты", Новый ТаблицаЗначений);
			
			НаборСвойств.ДополнительныеРеквизиты.Колонки.Добавить("Свойство");
			
			Выборка = РезультатЗапросаПоСвойствамНоменклатуры.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				СоответствиеНаименование.Вставить(Выборка.НазначениеНаименование);
				
				НовыеСвойство = НаборСвойств.ДополнительныеРеквизиты.Добавить();
				НовыеСвойство.Свойство = Выборка.Свойство;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Формирование наименований...
		Индекс = 0;
		СоздатьВидНоменклатуры_Наименование = "";
		Для Каждого КлючИЗначение Из СоответствиеНаименование Цикл
			Индекс = Индекс + 1;
			Если Индекс = 1 Тогда
				СоздатьВидНоменклатуры_Наименование = КлючИЗначение.Ключ;	
			Иначе
				СоздатьВидНоменклатуры_Наименование = СоздатьВидНоменклатуры_Наименование + ", ";
				СоздатьВидНоменклатуры_Наименование = СоздатьВидНоменклатуры_Наименование + КлючИЗначение.Ключ;
			КонецЕсли;
		КонецЦикла;
		СоздатьВидНоменклатуры_Наименование = СоздатьВидНоменклатуры_Наименование + ?(СоздатьВидНоменклатуры_Номенклатура.ВестиУчетПоХарактеристикам, " с характ.", "");
		// Длина наименования не должна превышать 50 символов.
		СоздатьВидНоменклатуры_Наименование = СокрЛП(Лев(СоздатьВидНоменклатуры_Наименование, 50));
		
		Если ЕстьНаборСвойств Тогда
			НаборСвойств.Вставить("Наименование", СоздатьВидНоменклатуры_Наименование);
		КонецЕсли;
		Если ЕстьНаборСвойствХарактеристик Тогда
			НаборСвойствХарактеристик.Вставить("Наименование", СоздатьВидНоменклатуры_Наименование + "(Для характеристик)");
		КонецЕсли;
		
		// Возможно наименование нужно изменить...
		Значение.Вставить("Наименование", СоздатьВидНоменклатуры_Наименование);
		
		Параметры.КешВидовНоменклатуры.Вставить(СоздатьВидНоменклатуры_Номенклатура, Значение);
		
	КонецЕсли;
	
КонецЕсли;
