Запрос = Новый Запрос("
|ВЫБРАТЬ
|	ДенежныеСредстваОстатки.Организация         КАК Организация,
|	ДенежныеСредстваОстатки.БанковскийСчетКасса КАК Касса,
|	ДенежныеСредстваОстатки.СуммаОстаток        КАК Сумма
|ИЗ
|	РегистрНакопления.ДенежныеСредства.Остатки(&ДатаОстатков, ВидДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)) КАК ДенежныеСредстваОстатки
|
|УПОРЯДОЧИТЬ ПО
|	ДенежныеСредстваОстатки.БанковскийСчетКасса.Наименование 	
| 
|ИТОГИ ПО
|	Организация
|");

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("Кассы");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков); 
	НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация; 
	
	НоваяСтрока.Кассы = Новый ТаблицаЗначений;
	НоваяСтрока.Кассы.Колонки.Добавить("Касса");
	НоваяСтрока.Кассы.Колонки.Добавить("Сумма");
	
	ВыборкаДетальныеЗаписи = ВыборкаПоОрганизациям.Выбрать(); 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.Сумма < 0.00 Тогда
			СтрокаСообщения = "Обнаружены отрицательные остатки по кассе ""%1"" при выгрузке по правилу: ""%2""";
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1", ВыборкаДетальныеЗаписи.Касса);
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2", "Денежные средства в кассах");
			Сообщить(СтрокаСообщения, СтатусСообщения.ОченьВажное);
			Продолжить;
		КонецЕсли;
		
		НоваяСтрокаКассы = НоваяСтрока.Кассы.Добавить();	
		НоваяСтрокаКассы.Касса = ВыборкаДетальныеЗаписи.Касса;
		НоваяСтрокаКассы.Сумма = ВыборкаДетальныеЗаписи.Сумма;
		
	КонецЦикла;
	
	Если НоваяСтрока.Кассы.Количество() = 0 Тогда
		ВыборкаДанных.Удалить(НоваяСтрока);	
	КонецЕсли;
	
КонецЦикла;
