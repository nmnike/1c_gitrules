Запрос = Новый Запрос("
|ВЫБРАТЬ
|	&ДатаОстатков                                                                                                                              КАК ДатаПлатежа,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Организация                                                                         КАК Организация,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом                                                       КАК ДокументРасчетовСКонтрагентом,
|	ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом  КАК Документ.ПоступлениеТоваровУслуг).Дата  КАК ДатаРасчетногоДокумента,
|	ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом  КАК Документ.ПоступлениеТоваровУслуг).Номер КАК НомерРасчетногоДокумента,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент                                                                          КАК Партнер,
|	-ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток                                                         КАК Сумма,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов                                             КАК Валюта
|ИЗ                                                                                   
|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиПоДокументамРасчетов.Остатки(&ДатаОстатков, ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)) КАК ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки
|ГДЕ
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток < 0
|	И ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.УпрУчет
|	И ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ВедениеВзаиморасчетов <> ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам)
|	И ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслуг 
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	&ДатаОстатков                                                                                                                                  КАК ДатаПлатежа,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Организация                                                                             КАК Организация,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом                                                           КАК ДокументРасчетовСКонтрагентом,
|	ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом  КАК Документ.ПоступлениеТоваровУслугВНТТ).Дата  КАК ДатаРасчетногоДокумента,
|	ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом  КАК Документ.ПоступлениеТоваровУслугВНТТ).Номер КАК НомерРасчетногоДокумента,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент                                                                              КАК Партнер,
|	-ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток                                                             КАК Сумма,
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов                                                 КАК Валюта
|ИЗ                                                                                   
|	РегистрНакопления.ВзаиморасчетыСКонтрагентамиПоДокументамРасчетов.Остатки(&ДатаОстатков, ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)) КАК ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки
|ГДЕ
|	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток < 0
|	И ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.УпрУчет
|	И ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ВедениеВзаиморасчетов <> ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам)
|	И ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ 
|
|УПОРЯДОЧИТЬ ПО
|	Партнер 	
|
|ИТОГИ ПО
|	Организация	
|");

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("РасчетыСПартнерами");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков); 
	НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация; 
	
	НоваяСтрока.РасчетыСПартнерами = Новый ТаблицаЗначений;
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("Партнер");
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("Контрагент");
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("ДатаРасчетногоДокумента");
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("НомерРасчетногоДокумента");
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("Сумма");
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("Валюта");
	НоваяСтрока.РасчетыСПартнерами.Колонки.Добавить("ДатаПлатежа");
	
	ВыборкаДетальныеЗаписи = ВыборкаПоОрганизациям.Выбрать(); 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрокаРасчетыСПартнерами = НоваяСтрока.РасчетыСПартнерами.Добавить();	
		НоваяСтрокаРасчетыСПартнерами.Партнер                  = ВыборкаДетальныеЗаписи.Партнер;
		НоваяСтрокаРасчетыСПартнерами.Контрагент               = ВыборкаДетальныеЗаписи.Партнер;
		НоваяСтрокаРасчетыСПартнерами.ДатаРасчетногоДокумента  = ВыборкаДетальныеЗаписи.ДатаРасчетногоДокумента;
		НоваяСтрокаРасчетыСПартнерами.НомерРасчетногоДокумента = ВыборкаДетальныеЗаписи.НомерРасчетногоДокумента;
		НоваяСтрокаРасчетыСПартнерами.Сумма                    = ВыборкаДетальныеЗаписи.Сумма;
		НоваяСтрокаРасчетыСПартнерами.Валюта                   = ВыборкаДетальныеЗаписи.Валюта;
		НоваяСтрокаРасчетыСПартнерами.ДатаПлатежа              = ВыборкаДетальныеЗаписи.ДатаПлатежа;
		
	КонецЦикла;
		
КонецЦикла;
